StatementList::= Statement (';' Statement)* (';' S?)?
Statement::= S? ( AlterIndex | AlterTable | Constraint | Create | CreateIndex
   | Delete | DropIndex | DropSchema | DropTable | Explain
   | InsertSelect | InsertValues | Select ) S?

/********************/
Comment ::= '/*' ([^*+]+ | '*' - '*/')? ([^*]+ | '*' - '*/')* '*/' 
   | ('--' | '//') $skipToNextLine
S::= ([#9#10#13 ]+ | Comment)+
Comma ::= S? ',' S?
QuotedName ::= '"' ('""' | [^"])+ '"'
Keywords ::= 'ALL'% | 'AS'% | 'ASC'% | 'BETWEEN'% | 'BY'%
   | 'CASE'% | 'DESC'% | 'DISTINCT'% | 'ELSE'% | 'END'% | 'EXISTS'%
   | 'FALSE'% | 'FIRST'% | 'FROM'% | 'GROUP'% | 'HAVING' | 'IN'% | 'IS'%
   | 'LAST'% | 'LIKE'% | 'LIMIT'% | 'NOT'% | 'NULL'% | 'OR'%
   | 'ORDER'% | 'SELECT'% | 'THAN'% | 'TRUE'% | 'WHERE'% | 'WHEN'%
Name ::= (([a-zA-Z] | '_') ([a-zA-Z0-9] | '_')*) - Keywords | QuotedName
String ::= "'" ("''" | [^'])+ "'"
Numeric ::= ('-' S?)? [0-9]+ ('.' [0-9]+)?
Boolean ::= 'TRUE'% | 'FALSE'%
Constant ::= String | Numeric | Boolean
Hint::= '/*' S? '+' S? (Name | Function) S? '*/' S?
SelectExpression::= S? ('*' | Name '.' S? '*' | Term (S? 'AS'% S? Name)?)
Order::= Expression (S? ('ASC'% | 'DESC'%)? S? )
   ( S? 'NULLS'% S? ('FIRST'% | 'LAST'%) S? )?
Value::= String | Numeric | Boolean | 'NULL'% | (Name S? '.' S?)? Name
BindParameter::= '?' | ':' S? Numeric
RowValueConstructor::= S? '(' S? Term (Comma Term)* S? ')' S?
Case::= S? 'CASE'% S  Term 'WHEN'% S Expression
   S? 'THEN'% Term (S? 'ELSE'% Expression)? S? 'END'%
CaseWhen::= S? 'CASE'% S 'WHEN'% S Expression
   S? 'THEN'% Term (S? 'ELSE'% Term)? S? 'END'%
Expression::= AndCondition (S? 'OR'% S AndCondition)*
AndCondition::= Condition (S? 'AND'% S Condition)*
Factor::= Term (S? ('*' | '/') S? Term)*
Summand::= Factor (S? ('+' | '-') S? Factor)*
Term::= Name (S? '.' S? Name){0, 2} | BindParameter | Value | Function
   | Case | CaseWhen | '(' S? Operand S? ')' | RowValueConstructor
Operand::= Summand (S? '|' S? '|' S? Summand)*
Condition::= (Operand (
   (Compare Operand)
   | (S? 'NOT'% S?)? S? 'IN'% S? '(' S? Operand (Comma Operand)* S? ')'
   | (S? 'NOT'% S?)? S? 'LIKE'% S? Operand
   | (S? 'NOT'% S?)? S? 'BETWEEN'% S? Operand S? 'AND'% S? Operand
   | S? 'IS'% S? ('NOT'% S?)? S? 'NULL')? )
   | S? 'NOT'% S? Expression 
   | S? '(' S? Expression S? ')'?
Compare::= S? ('<'S?'>' | '<'S?'=' | '>'S?'=' | '=' | '<' | '>' | '!'S?'=') S?
TableExpressionItem ::= S? Name (S? '.' S? Name)? (S? 'AS'% S? Name)?
TableExpression::= TableExpressionItem (Comma TableExpressionItem)*
ColumnRef::= (Name S? '.' S?)? Name
TableRef::= (Name S? '.' S?)? Name
ColumnDef::= ColumnRef (S DataType)?
   ((S? 'NOT'%)? S? 'NULL'%)? (S? 'PRIMARY'% S 'KEY'% (S? ('ASC'%|'DESC'%))?)?
DataType::= 'BINARY'% S? '(' S? Numeric S? ')'
   | 'CHAR'% S? '(' S? Numeric S? ')'
   | 'VARCHAR'% S? '(' S? Numeric S? ')'
   | 'BIGINT'% | 'DECIMAL'% | 'DATE'% | 'DOUBLE'% | 'FLOAT'% | 'INTEGER'%
   | 'SMALLINT'% | 'TIME'% | 'TIMESTAMP'% | 'TINYINT'%
   | 'UNSIGNED_DOUBLE'% | 'UNSIGNED_FLOAT'% | 'UNSIGNED_INT'% | 'UNSIGNED_LONG'%
   | 'UNSIGNED_SMALLINT'% | 'UNSIGNED_TINYINT'% | 'VARBINARY'%
Options::= (Name S? '.' S?)? Name S? '=' S? SplitPoint (Comma SplitPoint)*
SplitPoint::= Value | BindParameter
Function::= 'COUNT'% S? '(' S? ('DISTINCT'% S?)? ('*' | Term) S? ')'
   | ('PERCENTILE_CONT'% | 'PERCENTILE_DISC'% | 'PERCENT_RANK '%) S?
      '(' S? Numeric S? ')' S? 'WITHIN'% S 'GROUP'% S?
      '(' S? 'ORDER'% S 'BY' S? Term S?
      ('ASC%'|'DESC%') S? ')'
   | ('CURRENT_DATE'% | 'CURRENT_TIME') S? '(' S? ')'%
   | 'TO_NUMBER'% S? '(' ? Term (Comma String)? S? ')'
   | 'COALESCE'% S? '(' ? Term (Comma Term)? S? ')'
   | 'SUBSTR'% S? '(' ? Term (Comma Term){1, 2} S? ')'
   | 'REGEXP_SUBSTR'% S? '(' ? Term Comma String (Comma Numeric)? S? ')'
   | 'REGEXP_REPLACE'% S? '(' ? Term Comma String (Comma String)? S? ')'
   | ('ROUND'% | 'TRUNC'%) S? '(' S?
      "'" S? ('DAY'% | 'HOUR'% | 'MINUTE'% | 'SECOND'% | 'MILLISECOND'%) S? "'"
      Comma Numeric S? ')'
   | 'TO_CHAR'% S? '(' ? Term (Comma String)? S? ')'
   | 'TO_DATE'% S? '(' ? Term (Comma String)? S? ')'
   | ('AVG'% | 'MAX'% | 'MIN'% | 'SUM'% | 'STDDEV_POP'% | 'STDDEV_SAMP'%
      | 'MD5'% | 'INVERT'% | 'TRIM'% | 'LTRIM'% | 'RTRIM'% | 'LENGTH'%
      | 'UPPER'% | 'LOWER'% | 'REVERSE'% | 'TO_NUMBER'%)
      S? '(' ? Term S? ')'
ConstraintColumn::= 'PRIMARY'% S 'KEY'% S? '(' S? Name S? ('ASC'% | 'DESC'%)?
   (Comma Name S? ('ASC'% | 'DESC'%)?)* S? ')'

/**** Statements ****/
AlterIndex::= 'ALTER'% S 'INDEX'% (S 'IF'% S 'EXISTS'%)? S? Name S?
   'ON'% S? TableRef S? ('DISABLE'% | 'REBUILD'% | 'UNUSABLE'% | 'USABLE'%)
AlterTable::= 'ALTER'%S'TABLE'% S? TableRef (
   (S 'ADD'% (S 'IF'% S 'NOT'% S 'EXISTS'%)? S? ColumnDef (S? Options)?)
   | (S 'DROP'% S 'COLUMN'% (S 'IF'% S 'EXISTS'%)? S? ColumnRef)
   | (S 'SET'% S? Options) )
Constraint::= 'CONSTRAINT'% S? Name S? 'PRIMARY'% S 'KEY'% S?
   '(' S? Name S? (('ASC'%|'DESC'%) S?)? (Comma Name S? ('ASC'%|'DESC'%)?)* ')'
Create::= 'CREATE'% S ('TABLE'% | 'VIEW'%) S? ('IF'% S 'NOT'% S 'EXISTS'%)?
    S? TableRef S? '(' S? CreateDef (Comma CreateDef)* S? ')' S?
    Options? ('SPLIT'% S 'ON'% S? '(' S? SplitPoint (Comma SplitPoint)* S? ')')?
CreateDef::= (('PRIMARY'% S 'KEY'% S?) | 'UNIQUE'%)?
   S? '(' S? ColumnDef (Comma ColumnDef)* S? ')' S?
   | 'FOREIGN'% S 'KEY' S? '(' S? ColumnDef (Comma ColumnDef)* S? ')' S?
      (S? 'REFERENCES' S? ColumnRef S? '(' S? Name (Comma Name)*  S? ')')?
   | ColumnDef
CreateIndex::= 'CREATE'% S 'INDEX'% (S 'IF'% S 'NOT'% S 'EXISTS'%)? S? Name
   S? 'ON'% S? TableRef S? '(' S? ColumnRef (S? ('ASC'% | 'DESC'%))?
      (Comma ColumnRef (S? ('ASC'% | 'DESC'%)?))* S? ')'
   (S? 'INCLUDE'% S?'(' S? ColumnRef (Comma ColumnRef)* S? ')')?
   (S? Options? (S? 'SPLIT'%S'ON'% S?
      '(' S? SplitPoint (Comma SplitPoint)* S?')')?)?
Delete::= 'DELETE'% S? Hint? 'FROM'% S? TableExpression?
   ( S? 'WHERE'% S? Expression )?
   ( S? 'ORDER'% S 'BY'% S? Order (Comma Order)* )?
   ( S? 'LIMIT'% S? (Numeric | BindParameter) )?
DropIndex::= 'DROP'% S 'INDEX'% (S 'IF'% S 'EXISTS'%)? S? Name S? 'ON'% S? TableRef
DropSchema::= 'DROP'% S 'SCHEMA'% (S ('IF'% S 'EXISTS'%))? S? TableRef (S? 'RESTRICT'%)?
DropTable::= 'DROP'% S ('TABLE'% | 'VIEW'%) S? ('IF'% S 'EXISTS'%)? TableRef
Explain::= 'EXPLAIN'% S? (Select | InsertSelect | Delete)
InsertSelect::= ('UPSERT'% | 'INSERT'%) S? Hint? S? 'INTO'% S? TableExpression S?
   ('(' S? (ColumnRef|ColumnDef) (Comma (ColumnRef|ColumnDef))* S? ')' S?)?
   Select
InsertValues::= ('UPSERT'% | 'INSERT'%) S 'INTO'% S? TableRef S?
   ('(' S? (ColumnRef | ColumnDef) (Comma (ColumnRef|ColumnDef))* S? ')' S?)?
   'VALUES'% S? '(' S? ('(' S? Select S?')'|Term) (Comma ('(' S? Select S?')'|Term))* S? ')'
Select::= 'SELECT'% S? Hint? (S? ('DISTINCT'% | 'ALL'%) S?)?
   SelectExpression (Comma SelectExpression)*
   S? 'FROM'% S? TableExpression S? ( '(' S? ColumnDef S? ')')?
   (S? 'WHERE'% S? Expression)?
   (S? 'GROUP'% S 'BY'% S? Expression (Comma Expression)*)?
   (S? 'HAVING'% S? Expression)?
   (S? 'ORDER'% S 'BY'% S? Order (Comma Order)* )?
   (S? 'LIMIT'% S? (Numeric | BindParameter))?
